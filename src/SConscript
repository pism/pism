import os
Import('env prefix pism_libs')

libpism = env.SharedLibrary('pism', Split("""grid.cc materials.cc nc_util.cc iceModel.cc
iMbasal.cc iMbeddef.cc iMgrainsize.cc iMIO.cc iMIOnetcdf.cc iMmacayeal.cc
iMtemp.cc iMutil.cc iMvelocity.cc iMviewers.cc iMdefaults.cc iMoptions.cc
iMregrid.cc extrasGSL.cc cubature.c pism_signal.c"""))

flowTable = env.Program('flowTable', 'flowTable.cc', LIBS = ['pism'] + pism_libs)
pismr = env.Program('pismr.cc', LIBS = ['pism'] + pism_libs)
pisms = env.Program('pisms',
      ['iceEISModel.cc', 'iceHEINOModel.cc', 'iceROSSModel.cc', 'pisms.cc'], 
      LIBS = ['pism'] + pism_libs)
shelf = env.Program('shelf', 'shelf.cc', LIBS = ['pism'] + pism_libs)

libpismtests = env.SharedLibrary('pismtests', Split("""exact/exactTestsABCDE.c exact/exactTestsFG.c 
exact/exactTestH.c exact/exactTestI.c"""))

simpleISO = env.Program('simpleISO', 'exact/simpleISO.c', LIBS = ['tests', 'm'])
simpleFG = env.Program('simpleFG', 'exact/simpleFG.c', LIBS = ['tests', 'm'])
simpleI = env.Program('simpleI', 'exact/simpleI.c', LIBS = ['tests', 'm'])

pismv = env.Program('pismv', ['iceCompModel.cc', 'iceExactStreamModel.cc', 'pismv.cc'],
                     LIBS = ['pism', 'pismtests'] + pism_libs)

pant = env.Program('pant.cc', LIBS= ['pism'] + pism_libs)
pgrn = env.Program(['pgrn.cc', 'iceGRNModel.cc'], LIBS= ['pism'] + pism_libs)

#Default(flowTable, run, simplify, verify, simpleISO, simpleFG, simpleI, shelf, pant)
Default(pismr, pisms, pismv, pant, pgrn)

env.Alias('pismr', pismr)
env.Alias('pisms', pisms)
env.Alias('pismv', pismv)
env.Alias('pant', pant)
env.Alias('pgrn', pgrn)

# Install
bindir = os.path.join(prefix, 'bin')
libdir = os.path.join(prefix, 'lib')
env.Install(bindir, [pismr, pisms, pismv, pant, pgrn])
env.Install(libdir, [libpism, libpismtests])
ib = env.Alias('install-bin', bindir)
il = env.Alias('install-lib', libdir)
env.Alias('install', [ib, il])
