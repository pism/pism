/* -*- mode: maxima -*- */
assume(dx > 0, dy > 0);

/* Coordinates of the nodes of a Q1 rectangular element aligned with the coordinate system. */
pts : [
  [-dx/2, -dy/2],
  [ dx/2, -dy/2],
  [ dx/2,  dy/2],
  [-dx/2,  dy/2]]$

/* Short-cuts for nodes */
P0 : pts[1]$
P1 : pts[2]$
P2 : pts[3]$
P3 : pts[4]$

/* Four triangles embedded in a rectangular element */
tris : [
  [P0, P1, P3],
  [P1, P2, P0],
  [P2, P3, P1],
  [P3, P0, P2]
]$

/* xi, eta coordinates of the nodes of the reference element */
P : [[0, 0], [1, 0], [0, 1]];

/* P1 shape functions, using the reference element defined above */
chi[1](xi,eta) := 1 - xi - eta$
chi[2](xi,eta) := xi$
chi[3](xi,eta) := eta$

/* Map from the reference element. */
x(xi,eta) := 'sum(x[j]*chi[j](xi,eta), j, 1, 3)$
y(xi,eta) := 'sum(y[j]*chi[j](xi,eta), j, 1, 3)$

/* Compute the Jacobian of the mapping from the reference element */
J : matrix(
  ['diff(x(xi,eta), xi), 'diff(y(xi,eta), xi)],
  ['diff(x(xi,eta), eta),'diff(y(xi,eta), eta)])$

/* Jacobian, simplified. */
J_s : ratsimp(ev(J, nouns))$

/* Coordinates of the nodes of physical elements embedded in Q1
elements aligned with coordinate axes. */
eqs[k] := block([T],
  T : tris[k],
  [
  x[1] = T[1][1],
  y[1] = T[1][2],
  x[2] = T[2][1],
  y[2] = T[2][2],
  x[3] = T[3][1],
  y[3] = T[3][2]
  ])$

/* Jacobian of the map from the reference P1 element to the k-th triangle */
J[k] := ev(J_s, eqs[k]);
